#pragma experiment("FOR_LOOP")
#pragma experiment("BRIDGE_CONNECT")
#pragma experiment("TRAITS")

# Standard library imports
import ElectricPower
import Resistor
import ElectricLogic
import I2C
import I2S
import can_bridge_by_name

# Package imports from https://packages.atopile.io/
from "atopile/usb-connectors/usb-connectors.ato" import USBCConn
from "atopile/ti-tps63020/ti-tps63020.ato" import TPS63020DSJR
from "atopile/ti-bq25185/ti-bq25185.ato" import BQ25185_driver
from "atopile/batteries/eemb_battery_lp402535.ato" import LP402535_Example
from "atopile/esp32/esp32_c3_mini.ato" import ESP32_C3_MINI_1_driver, ESP32_C3_MINI_1_model
from "atopile/addressable-leds/sk6805-ec20.ato" import SK6805EC20_driver
from "atopile/microphones/tdk_invensense_ics_43434.ato" import TDK_InvenSense_ICS_43434_driver
from "atopile/st-lsm6ds3/st-lsm6ds3.ato" import ST_LSM6DS3_driver

module LED_BADGE:
    """
    LED Badge with:
    - ESP32
    - 10x10 LED matrix, 100 LEDs
    - USB-C connector
    - Battery connector
    - Battery charger with power path
    - Buck-boost converter to 3.3V
    - Accelerometer
    """
    micro = new ESP32_C3_MINI_1_driver
    micro.esp32_module -> ESP32_C3_MINI_1_model
    microphone = new TDK_InvenSense_ICS_43434_driver
    imu = new ST_LSM6DS3_driver
    usb_c = new USBCConn
    buck_boost = new TPS63020DSJR
    charger = new BQ25185_driver
    battery = new LP402535_Example
    led_grid = new SK6805EC20_grid100x100

    charger.charge_current = battery.battery.capacity / 2h +/- 10%

    # Power
    power_3v3 = new ElectricPower

    # Power path: USB 5V -> Charger -> Battery / System -> BuckBoost -> 3V3 rail
    usb_c.usb2.usb_if.buspower ~> charger ~> battery.power_battery
    charger.power_sys ~> buck_boost ~> power_3v3

    # Configure Power
    usb_c.usb2.usb_if.buspower.voltage = 5V +/- 5%
    charger.input_current_limit = 500mA
    buck_boost.v_out = 3.3V +/- 5%
    battery.battery.capacity = 300mAh
    assert battery.battery.voltage is 3V to 4.2V
    assert charger.battery_voltage_limit_high is 4.2V
    assert charger.battery_voltage_limit_low is 3.0V

    # Connect 3.3 V rail to loads
    power_3v3 ~ micro.power
    power_3v3 ~ microphone.power
    power_3v3 ~ imu.power_core
    power_3v3 ~ imu.power_io
    power_3v3 ~ led_grid.power

    # I2C for IMU
    i2c_bus = new I2C
    micro.esp32_module.esp32.i2c ~ i2c_bus
    i2c_bus ~ imu.i2c

    # I2S for microphone (mapped via GPIOs 3/4/5)
    i2s_bus = new I2S
    i2s_bus.sck ~ micro.esp32_module.gpio[3]
    i2s_bus.ws  ~ micro.esp32_module.gpio[4]
    i2s_bus.sd  ~ micro.esp32_module.gpio[5]
    i2s_bus ~ microphone.i2s

    # LED Data
    led_grid.data_in ~ micro.esp32_module.gpio[6]

    # Net naming
    micro.esp32_module.gpio[6].line.override_net_name = "led_data"
    i2s_bus.ws.line.override_net_name = "i2s_ws"
    i2s_bus.sck.line.override_net_name = "i2s_sck"
    i2s_bus.sd.line.override_net_name = "i2s_sd"
    i2c_bus.scl.line.override_net_name = "i2c_scl"
    i2c_bus.sda.line.override_net_name = "i2c_sda"
    power_3v3.hv.override_net_name = "power_3v3"
    power_3v3.lv.override_net_name = "gnd"


module SK6805EC20_strip10:
    """
    Row of 10 SK6805 addressable LEDs behaving like a single LED that can be bridged by name.
    """
    # Instantiate 10 individual LED drivers
    leds = new SK6805EC20_driver[10]

    # Shared power rail for all LEDs
    power = new ElectricPower
    for led in leds:
        power ~ led.power

    # Serial data interface (input → chain → output)
    data_in  = new ElectricLogic
    data_out = new ElectricLogic

    # Tie logic references to the power rail
    power ~ data_in.reference
    power ~ data_out.reference

    # Chain the LEDs' data lines
    data_in ~> leds[0] ~> leds[1] ~> leds[2] ~> leds[3] ~> leds[4] ~> leds[5] ~> leds[6] ~> leds[7] ~> leds[8] ~> leds[9] ~> data_out

    # Expose the strip as a single bridgable element
    trait can_bridge_by_name<input_name = "data_in", output_name = "data_out">


module SK6805EC20_grid100x100:
    """
    10 × 10 LED matrix (100 LEDs) constructed from 10 rows of 10 LEDs.
    Presents itself as one bridgable LED for easy chaining.
    """
    rows = new SK6805EC20_strip10[10]

    # Shared power rail for all rows
    power = new ElectricPower
    for row in rows:
        power ~ row.power

    # Serial data interface for the whole matrix
    data_in  = new ElectricLogic
    data_out = new ElectricLogic

    power ~ data_in.reference
    power ~ data_out.reference

    # Chain the rows
    data_in ~> rows[0] ~> rows[1] ~> rows[2] ~> rows[3] ~> rows[4] ~> rows[5] ~> rows[6] ~> rows[7] ~> rows[8] ~> rows[9] ~> data_out

    trait can_bridge_by_name<input_name = "data_in", output_name = "data_out">
