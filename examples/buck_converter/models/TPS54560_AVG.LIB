*$
* TPS54560
*****************************************************************************
* (C) Copyright 2013 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: TPS54560
* Date: 30SEP2013
* Model Type: AVERAGE
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: TPS54560EVM-515
* EVM Users Guide: SLVU863Â–February 2013
* Datasheet: SLVSBN0-March 2013
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
.SUBCKT TPS54560_AVG COMP GND SW VIN FB  PARAMS: L=27u FS=415k
G_G1         GND COMP N14514060 FB 350u
V_V2         N14514060 GND 0.8
R_R5         GND COMP  103e6 TC=0,0 
C_C2         GND COMP  5.71p  TC=0,0 
X_U2         VIN SW GND COMP DC DCMCCMSWITCH PARAMS:  L={L} FS={FS} RI={1/17}
.ENDS TPS54560_AVG
*$
.SUBCKT DCMCCMSWITCH a c p vc dc params: L=35u Fs=200k Ri=1
Esa Sa 0 value={5.2*{Fs}*{Ri}}
Edc dcx 0 value = { v(d2)*v(cx,p)/(v(a,p)-v(cx,p)+1u) }
EFsv Fsv 0 value={Fs}
Xdc dcx dc Fsv L1 params: clampH=0.98
Ed2 d2X 0 value = { (2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u)) - V(dc) }
Xd2 d2X dc d2 ld2
GIap a p value = { (V(dc)/(V(dc)+V(d2)+1u))*I(VM) }
GIpc p cx value = { V(vc)/{Ri} }
GImju cx p value = {V(Sa)*V(dc)/({Ri}*{Fs}) + (v(cx,p)/{L})*
+ V(d2)*{1/Fs}*(1-(V(dc)+V(d2))/2) }
VM cx c
XC1 cx p mode vdcap;
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, 1p , {4/((L)*(6.28*Fs)^2)}) }
.ENDS
*$
.SUBCKT VDCAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.subckt L1 d dc Fsv params: clampH=0.89
Gd 0 dcx VALUE = {V(d)*100u}
Rdc dcx 0 10k
E_V1 clpn 0  value ={ 130e-9*V(Fsv) }
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={V(dcx)}
.model dclamp d n=0.01 Rs=100m 
.ENDS
*$
.subckt Ld2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 7m
E2 clpp 0 Value = {1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={V(d2cx)}
.model dclamp d n=0.01 Rs=100m 
.ENDS
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L     IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS
*$
.SUBCKT CESR 1 2 PARAMS: C=10u ESR=2m
C1 1 INT1 {C}
R1 INT1 0 {ESR}
.ENDS CESR
*$
